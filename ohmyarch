#!/usr/bin/env zsh

setopt errexit nounset pipefail extendedglob

function setup() {
  sudo pacman -S --noconfirm --needed $(basename -a $@)
}

function backup() {
  for filepath ($(envsubst < .archive/file)) {
    until [[ $filepath == / ]] echo ${filepath::=$filepath:h}
  } | sort | uniq | getfacl -p - > .archive/facl
  envsubst < .archive/file | sort | getfacl -pR - >> .archive/facl
  envsubst < .archive/file | rsync -ar --files-from=- / .archive
}

function restore() {
  envsubst < .archive/file | sudo rsync -rl --files-from=- .archive /
  sudo setfacl --restore=.archive/facl
}

function clean() {
  rm -rf .archive/^file
}

function main() {
  for app (${2:-$(basename -a */)} ${@:3}) {
    pushd $app
    [[ -n */(#qN) ]] && main $1 || source $1
    popd
  }
}

main $@
